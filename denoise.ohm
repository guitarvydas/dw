// References:

// https://cs.lmu.edu/~ray/notes/xmlgrammar/

// https://www.tutorialspoint.com/xml/xml_syntax.htm

//


XML {
  parser = xml

  xml = prolog? root
  prolog = ws* "<?" (~"?>" any)+ "?>" ws*
  root = nestedElement

  nestedElement =
    | ws* "<" nameAndAttributesAndSpaces ">" element+ "</" name ">" ws*

  element =
    | ws* nestedElement -- nestedElement
    | ws* bottomElement -- bottomElement
    | stuff+ -- other

  bottomElement = "<" nameAndAttributesAndSpaces "/>" ws*

  attribute = name "=" string

  name = nameCharacter+
  nameCharacter = ~"<" ~">" ~"/" ~"&" ~ws ~dq ~ws ~"=" any
  ws = " " | "\n"

  nameAndAttributesAndSpaces = name wsattribute* ws*

  stuff = ~"<" ~">" ~"/" any

  string = dq (~dq any)* dq
  dq = "\""

  wsattribute = ws+ attribute

}

DENOISE <: XML {

  string := stringNumber | stringNoSpaces | stringWithSpaces
  stringNumber = dq nqNumber? dq
  stringNoSpaces = dq nqNoSpaces? dq
  stringWithSpaces = dq nqWithSpaces? dq
  nqNumber = (~dq ~space integerChar)+
  nqNoSpaces = (~dq ~space any)+
  nqWithSpaces = (~dq any)+
  integerChar = digit | "." | "-" | "+"

  nestedElement :=
    | ws* "<" name wsattribute* ws* "/>" ws* -- othersimple
    | ws* "<" "mxfile" wsattribute* ws* ">" element+ "</" name ">" ws* -- ignoreMxFile
    | ws* "<" "mxGraphModel" wsattribute* ws* ">" element+ "</" name ">" ws* -- ignoreMxGraphModel
    | ws* "<" name wsattribute* ws* ">" element+ "</" name ">" ws* -- other

  attribute :=
    | "style" "=" stylestring -- style
    | name "=" string -- other

  stylestring = dq styledecl+ dq
  styledecl =
    | styleid "=" ws* nonQuoted ";" -- valued
    | styleid ";" -- noValue
  styledeclchar = ~";" ~ws ~dq any
  nonQuoted = nqNumber | nqNoSpacesNoSemicolon | nqWithSpacesNoSemicolon | nqArray
  nqNoSpacesNoSemicolon = (~";" ~dq ~space any)+
  nqWithSpacesNoSemicolon = (~";" ~dq any)+
  nqArray = "[" arrayInner+ "]"
  arrayInner = arrayItem ","?
  arrayItem =
    | "[" arrayInner+ "]" -- rec
    | nqNumber -- bottom
  

  styleid = styleidchar+
  styleidchar = ~"=" ~";" ~ws ~dq any
}
